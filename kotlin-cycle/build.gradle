plugins {
    id 'com.jfrog.bintray' version '1.7'
    id "com.github.dcendents.android-maven" version "1.4.1"
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'org.jetbrains.dokka'

def _group = 'com.importre'
def _artifact = 'kotlin-cycle'
def _version = '0.1.10'
def _githubUrl = 'https://github.com/importre/kotlin-cycle'
def _desc = 'Cycle for Android (written in Kotlin)'

group _group
version _version

android {
    compileSdkVersion 23
    buildToolsVersion "24.0.1"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 23
        versionCode 1
        versionName _version
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'io.reactivex:rxjava:1.1.7'

    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    compile "com.android.support:appcompat-v7:$support_version"
    compile "com.android.support:cardview-v7:$support_version"
    compile "com.android.support:design:$support_version"
    compile "com.android.support:recyclerview-v7:$support_version"

    compile "com.jakewharton.rxbinding:rxbinding-appcompat-v7-kotlin:$rxbinding_version"
    compile "com.jakewharton.rxbinding:rxbinding-kotlin:$rxbinding_version"
    compile "com.jakewharton.rxbinding:rxbinding-support-v4-kotlin:$rxbinding_version"

    testCompile 'junit:junit:4.12'
    testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
}

repositories {
    jcenter()
}

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "source"
}

task javadocJar(type: Jar, dependsOn: 'dokka') {
    classifier = 'javadoc'
    from "$buildDir/dokka"
}

task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = 'javadoc'
}

artifacts {
    archives sourceJar, javadocJar
}

install {
    repositories.mavenInstaller {
        pom.project {
            name _artifact
            description _desc
            url _githubUrl
            inceptionYear '2016'

            packaging 'aar'
            groupId _group
            artifactId _artifact
            version _version

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }

            scm {
                connection = "${_githubUrl}.git"
                url _githubUrl

            }

            developers {
                developer {
                    name "Jaewe Heo"
                    id "importre"
                    email "jaeweheo@gmail.com"
                }
            }
        }
    }
}

bintray {
    user = project.hasProperty("user") ? project.user : ""
    key = project.hasProperty("key") ? project.key : ""
    configurations = ['archives']
    publish = true
    // dryRun = true

    pkg {
        repo = 'maven'
        name = _artifact
        licenses = ['Apache-2.0']
        vcsUrl = "${_githubUrl}.git"
        publicDownloadNumbers = true

        version {
            name = _version
            vcsTag = _version
            desc = _desc
            vcsTag = _version
            gpg {
                sign = true
            }
        }
    }
}
